# Maintainer: NTeditor <nteditor4@gmail.com>
# Contributor: Thule <vincenzo.frascino@proton.me>
# Contributor: Neptune <neptune650@proton.me>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Dag Odenhall <dag.odenhall@gmail.com>
# Contributor: Grigorios Bouzakis <grbzks@gmail.com>

pkgname=dwm-nteditor
pkgver=6.6
pkgrel=4
pkgdesc="A dynamic window manager for X"
url="https://dwm.suckless.org"
arch=('i686' 'x86_64' 'arm' 'armv7h' 'armv6h' 'aarch64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'libxft' 'freetype2' 'rofi' 'alacritty' 'ttf-cascadia-mono-nerd')
optdepends=(
    'lxqt-policykit: The LXQt policykit authentication agent'
    'dunst: Customizable and lightweight notification-daemon'
    'slstatus: A status monitor for window managers that use WM_NAME or stdin to fill the status bar'
    'dex: Program to generate and execute DesktopEntry files of type Application'
)
conflicts=('dwm' 'dwm-git')
provides=('dwm')
source=(dwm.desktop
        https://dl.suckless.org/dwm/dwm-$pkgver.tar.gz
        'systray.diff::https://dwm.suckless.org/patches/systray/dwm-systray-20230922-9f88553.diff'
        'pertag.diff::https://dwm.suckless.org/patches/pertag/dwm-pertag-20200914-61bb8b2.diff'
        xprofile.example
        config.h)
sha256sums=('bc36426772e1471d6dd8c8aed91f288e16949e3463a9933fee6390ee0ccd3f81'
            '7cfc2c6d9386c07c49e2c906f209c18ba3364ce0b4872eae39f56efdb7fc00a3'
            '29e357bd74f180016898fffaf219637952c777d26f5f2f161d0fbaea741908a1'
            '01cf4b387d2b2aee77e9c6288b5193b2cb5192405d1a9916ea82687152967fd1'
            '1ceb360faa82c5669c6616efdc7b05449ece8c35b19a337573ab88606cd63241'
            'SKIP')

prepare() {
  cd "$srcdir/dwm-$pkgver"
  if [[ -f "$srcdir/config.h" ]]; then
    cp -fv "$srcdir/config.h" config.h
  fi
  patch -Np1 -i "$srcdir/systray.diff"
  patch -Np1 -i "$srcdir/pertag.diff"
}

build() {
  cd "$srcdir/dwm-$pkgver"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 FREETYPEINC=/usr/include/freetype2
}

package() {
  cd "$srcdir/dwm-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
  install -Dm644 "$srcdir/dwm.desktop" "$pkgdir/usr/share/xsessions/dwm.desktop"
  install -Dm644 "$srcdir/xprofile.example" "$pkgdir/usr/share/doc/$pkgname/example/xprofile"
}
