# Maintainer: NTeditor <nteditor4@gmail.com>
# Contributor: Cp Dong <cp-dong at outlook dot com>

pkgname=slstatus-nteditor
pkgver=1.0
pkgrel=3
pkgdesc='A status monitor for window managers that use WM_NAME or stdin to fill the status bar'
arch=('i686' 'x86_64' 'arm' 'armv7h' 'armv6h' 'aarch64')
url='http://tools.suckless.org/slstatus/'
depends=('libx11' 'libpulse')
conflicts=('slstatus' 'slstatus-git')
provides=('slstatus')
license=('custom:ISC')
source=(https://dl.suckless.org/tools/slstatus-$pkgver.tar.gz
        'pulseaudio.diff::https://raw.githubusercontent.com/NTeditor/slstatus-pulseaudio/refs/heads/main/slstatus-pulseaudio-20251011.diff'
        config.h)
sha256sums=('6d6d0a16c08dd9d211172c30c4720701267a3f40cdc938db3f386f6a2b6cff54'
            'd2bca34c5bdbbee59b1c61f23f876bf6da027c71a8134bf51a6823b97d09e152'
            'SKIP')

prepare() {
  cd "$srcdir/slstatus-$pkgver"
  if [[ -f "$srcdir/config.h" ]]; then
    cp -fv "$srcdir/config.h" config.h
  fi
  patch -Np1 -i "$srcdir/pulseaudio.diff"
}

build() {
  cd "$srcdir/slstatus-$pkgver"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "$srcdir/slstatus-$pkgver"
  make PREFIX=/usr DESTDIR=$pkgdir install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}
